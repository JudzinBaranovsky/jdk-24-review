plugins {
    id 'org.springframework.boot' version "3.3.5"
    id 'io.spring.dependency-management' version "1.1.6"
    id "me.champeau.jmh" version "0.7.2"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    testImplementation "io.rest-assured:rest-assured:5.5.1"
}

var aotMode = System.getProperty("aotMode");
var extraBootRunJvmArgs = [];
if (aotMode == 'configure') {
    extraBootRunJvmArgs = ["-XX:AOTMode=record", "-XX:AOTConfiguration=app.aotconf"]
} else if (aotMode == 'record') {
    extraBootRunJvmArgs = ["-XX:AOTMode=create", "-XX:AOTConfiguration=app.aotconf", "-XX:AOTCache=main.aot"]
} else if (aotMode == 'run') {
    extraBootRunJvmArgs = ["-XX:AOTCache=main.aot"]
}

var jdk24Launcher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(24)
    vendor = JvmVendorSpec.matching("Oracle")
}
tasks.register('aotRun', JavaExec) {
    classpath = files(tasks.bootJar)
    jvmArgs = extraBootRunJvmArgs
    javaLauncher = jdk24Launcher
}

tasks.register('stopWebApp') {
    doLast {
        def request = URI.create("http://localhost:8080/actuator/shutdown")
                .toURL()
                .openConnection();
        request.setRequestMethod("POST");

        def responseCode = request.getResponseCode();
        println(responseCode);

        if (!responseCode.equals(200)) {
            throw new RuntimeException("failed to stop the application");
        }
    }
}

jmh {
    jvmArgs = extraBootRunJvmArgs
    jvm = jdk24Launcher.get().getExecutablePath().getAsFile().getAbsolutePath()
}
