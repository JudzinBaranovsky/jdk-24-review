plugins {
    id 'org.springframework.boot' version "3.3.5"
    id 'io.spring.dependency-management' version "1.1.6"
    id "me.champeau.jmh" version "0.7.2"
}

import me.champeau.jmh.ParameterConverter

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    testImplementation "io.rest-assured:rest-assured:5.5.1"
}

var jdk24Launcher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(24)
    vendor = JvmVendorSpec.matching("Oracle")
}

tasks.register('runAotLinkingConfig', JavaExec) {
    classpath = files(tasks.bootJar)
    jvmArgs = ["-XX:AOTMode=record", "-XX:AOTConfiguration=app.aotconf"]
    javaLauncher = jdk24Launcher
}

tasks.register("stopWebApp") {
    doFirst {
        def request = URI.create("http://localhost:8080/actuator/shutdown")
                .toURL()
                .openConnection();
        request.setRequestMethod("POST");

        def responseCode = request.getResponseCode();
        println(responseCode);

        if (!responseCode.equals(200)) {
            throw new RuntimeException("failed to stop the application");
        }
    }
}

tasks.register('runAotLinkingRecord', JavaExec) {
    classpath = files(tasks.bootJar)
    jvmArgs = ["-XX:AOTMode=create", "-XX:AOTConfiguration=app.aotconf", "-XX:AOTCache=main.aot"]
    javaLauncher = jdk24Launcher
}

tasks.register('runWithAot', JavaExec) {
    classpath = files(tasks.bootJar)
    jvmArgs = ["-XX:AOTCache=main.aot", "-XX:AOTMode=on"]
    javaLauncher = jdk24Launcher
}

tasks.register('runWithoutAot', JavaExec) {
    classpath = files(tasks.bootJar)
    javaLauncher = jdk24Launcher
}

tasks.register("coldStartTest") {
    doLast {
        var classPath = getObjects().fileCollection()
        classPath.from(tasks.bootJar)
        classPath.from(configurations.findByName("jmhRuntimeClasspath"))
        classPath.from(tasks.jmhJar)
        classPath.from(configurations.findByName("testRuntimeClasspath"))

        var jvmArgs = []
        ParameterConverter.collectParameters(tasks.jmh, jvmArgs)

        var tmpDir = getTemporaryDir()

        var run = getProviders().javaexec(spec -> {
            spec.setClasspath(classPath)
            spec.setWorkingDir(project.projectDir)
            spec.getMainClass().set("org.openjdk.jmh.Main")
            spec.args(jvmArgs)
            spec.systemProperty("java.io.tmpdir", tmpDir.getAbsolutePath())
            spec.environment(getEnvironment())
            spec.executable(jdk24Launcher.get().getExecutablePath().getAsFile())
        })

        run.getResult().get().assertNormalExitValue()
        var out = run.getStandardOutput().getAsText().get()
        var error = run.getStandardError().getAsText().get()
        println out
        println error
    }
}
tasks.findByName("coldStartTest").dependsOn(tasks.jmhJar)
